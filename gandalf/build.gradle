buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.btkelly:gnag:0.1.2'
    }
}

plugins {
    id "com.github.hierynomus.license" version "0.12.1"
}

apply plugin: 'com.android.library'
apply plugin: 'com.noveogroup.android.check'
apply plugin: 'gnag-plugin'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }

    lintOptions {
        abortOnError true
        warning 'InvalidPackage'
    }

    testOptions.unitTests {
        returnDefaultValues = true
        all {
            maxParallelForks = 2
            forkEvery = 150
            afterTest { descriptor, result ->
                println "Executing test for ${descriptor.name} with result: ${result.resultType}"
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

license {
    header = file('../Apache2License.txt')
    strictCheck true
}

gnag {
    gitHubRepoName 'btkelly/gandalf'
    failBuildOnError true
}

check {
    abortOnError false
    checkstyle {
        config project.file('config/checkstyle.xml')
    }

    findbugs {
        config hard()
    }

    pmd {
        config project.file('config/pmd.xml')
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.squareup.okhttp3:okhttp:3.0.1'

    testCompile 'junit:junit:4.12'
}

preBuild.dependsOn licenseFormat